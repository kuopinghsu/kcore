/****************************************************************************
 * boards/risc-v/kcore/kcore-board/scripts/ld.script
 *
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.  The
 * ASF licenses this file to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance with the
 * License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 ****************************************************************************/

OUTPUT_ARCH("riscv")

ENTRY(__start)

MEMORY
{
  kflash (rx) : ORIGIN = 0x80000000, LENGTH = 2M
  ksram (rwx) : ORIGIN = 0x80000000, LENGTH = 2M
}

SECTIONS
{
  .text :
  {
    _stext = .;
    *(.vectors)
    *(.text)
    *(.text.*)
    *(.gnu.linkonce.t.*)
    *(.rodata)
    *(.rodata.*)
    *(.gnu.linkonce.r.*)
    _etext = .;
  } > kflash

  .init_section :
  {
    _sinit = ABSOLUTE(.);
    KEEP(*(.init_array .init_array.*))
    _einit = ABSOLUTE(.);
  } > kflash

  .data :
  {
    _sdata = ABSOLUTE(.);
    *(.data)
    *(.data.*)
    *(.gnu.linkonce.d.*)
    CONSTRUCTORS
    . = ALIGN(4);
    _edata = ABSOLUTE(.);
  } > ksram AT > kflash

  _eronly = LOADADDR(.data);

  PROVIDE(__global_pointer$ = _sdata + ((_edata - _sdata) / 2));

  .bss :
  {
    _sbss = ABSOLUTE(.);
    *(.bss)
    *(.bss.*)
    *(.gnu.linkonce.b.*)
    *(COMMON)
    . = ALIGN(4);
    _ebss = ABSOLUTE(.);
    
    /* Spike/ISA simulator communication */
    . = ALIGN(8);
    tohost = .;
    . += 8;
    fromhost = .;
    . += 8;
  } > ksram

  /* Stabs debugging sections */

  .stab 0 : { *(.stab) }
  .stabstr 0 : { *(.stabstr) }
  .stab.excl 0 : { *(.stab.excl) }
  .stab.exclstr 0 : { *(.stab.exclstr) }
  .stab.index 0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment 0 : { *(.comment) }

  /* DWARF debug sections */

  .debug_abbrev 0 : { *(.debug_abbrev) }
  .debug_info 0 : { *(.debug_info) }
  .debug_line 0 : { *(.debug_line) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  .debug_aranges 0 : { *(.debug_aranges) }
}
